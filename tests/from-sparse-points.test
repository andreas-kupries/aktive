# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Images from sparse point locations

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax -- args issue

if 0 {syntax {
    points {args}
} {aktive image from sparse}}

# # ## ### ##### ######## ############# #####################
##

set points {{0 0} {4 3} {5 5} {6 2}}
set expected {
    1 0 0 0 0 0 0
    0 0 0 0 0 0 0
    0 0 0 0 0 0 1
    0 0 0 0 1 0 0
    0 0 0 0 0 0 0
    0 0 0 0 0 1 0
}

test aktive-image-from-sparse-points-2.0 "aktive image from sparse points" -body {
    astcl aktive image from sparse points {*}$points
} -match image -result [makei image::from::sparse::points \
			    0 0 7 6 1 [list points $points] \
			    $expected]

test aktive-image-from-sparse-points-2.1 "aktive image from sparse points, dag" -body {
    dag aktive image from sparse points {*}$points
} -match glob -result "image::from::sparse::points * {points {$points}}"

# - -- --- ----- -------- ------------- ---------------------
## Slice and check in all directions

lassign {7 6 1} w h d

for {set row 0} {$row < $h} {incr row} {
    test aktive-image-from-sparse-points-2.2.r$row "aktive image from sparse points, row slice" -body {
	dict get [astcl aktive op select y $row $row [aktive image from sparse points {*}$points]] pixels
    } -match pixels -result [slice-row $row $w $h $d $expected]
}

for {set col 0} {$col < $w} {incr col} {
    test aktive-image-from-sparse-points-2.2.c$col "aktive image from sparse points, column slice" -body {
	dict get [astcl aktive op select x $col $col [aktive image from sparse points {*}$points]] pixels
    } -match pixels -result [slice-col $col $w $h $d $expected]
}

for {set band 0} {$band < $d} {incr band} {
    test aktive-image-from-sparse-points-2.2.b$band "aktive image from sparse points, band slice" -body {
	dict get [astcl aktive op select z $band $band [aktive image from sparse points {*}$points]] pixels
    } -match pixels -result [slice-band $band $w $h $d $expected]
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
