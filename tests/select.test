# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Selection of rows, columns, bands

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

if 0 {syntax {
    x {first ?last? src}
    y {first ?last? src}
    z {first ?last? src}
} {aktive op select}}

# # ## ### ##### ######## ############# #####################
##
# input/gradient 3x4x2
##
##       0 ....   1......   2...... - 3 columns
##     --------  --------  --------
#	 1  1.5    2  2.5    3  3.5  0
#	 4  4.5    5  5.5    6  6.5  1
#	 7  7.5    8  8.5    9  9.5  2
#	10 10.5   11 11.5   12 12.5  3
##     --- ----  --- ----  --- ----  \ 4 rows
##      0. 1...   0. 1...   0. 1... - 2 bands
#
# Selecting column, row, or band 1

foreach {k op thing domain expected} {
    0 x column {x 1 y 0 width 1 height 4 depth 2} {
	2  2.5
	5  5.5
	8  8.5
	11 11.5
    }
    1 y row {x 0 y 1 width 3 height 1 depth 2} {
	4 4.5 = 5 5.5 = 6 6.5
    }
    2 z band {x 0 y 0 width 3 height 4 depth 1} {
	=  1.5 =  2.5 =  3.5 =
	=  4.5 =  5.5 =  6.5 =
	=  7.5 =  8.5 =  9.5 =
	= 10.5 = 11.5 = 12.5 =
    }
} {
    unset -nocomplain map
    lappend map /fun    $op
    lappend map /domain $domain
    lappend map /pixels $expected

    test aktive-op-select-${op}-2.0 "aktive op select $op" -body {
	astcl aktive op select $op 1 [aktive image gradient 3 4 2  1 12.5]
    } -match image -result [string map $map {
	type   op::select::/fun
	config {first 1 last 1}
	domain {/domain}
	pixels {/pixels}
    }]

    unset map

    test aktive-op-select-${op}-3.0 "aktive op select $op, first > range" -body {
	aktive op select $op 5 6 [aktive image constant 4 4 4 0]
    } -returnCodes error -result "First $thing >= 4"

    test aktive-op-select-${op}-3.1 "aktive op select $op, last > range" -body {
	aktive op select $op 3 6 [aktive image constant 4 4 4 0]
    } -returnCodes error -result "Last $thing >= 4"

    test aktive-op-select-${op}-3.2 "aktive op select $op, first > last" -body {
	aktive op select $op 2 1 [aktive image constant 4 4 4 0]
    } -returnCodes error -result "First $thing is after last"
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
