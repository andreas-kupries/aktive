# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Color conversion -- LCh / CIE Lab round trip
##         Both directions.

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    LCh src
} {aktive op color Lab to}

syntax {
    Lab src
} {aktive op color LCh to}

# # ## ### ##### ######## ############# #####################
## Round trip of the 216 web "standard" colors

foreach-color 3 [webcolor-table-fp] rgb bands {

    set lab    [aktive op color XYZ to Lab [aktive op color scRGB to XYZ $rgb]]
    set lbands [pixels/ $lab]
    set lb     ([join $lbands ,])

    # # ## ### ##### ######## ############# #####################

    # for the complementary round trip
    set lch    [aktive op color Lab to LCh $lab]
    set cbands [pixels/ $lch]
    set cb     ([join $cbands ,])

    incr k

    # # ## ### ##### ######## ############# #####################

    test aktive-op-color-Lab-to-LCh-2.${k}-$lb "aktive op color Lab to LCh, round trip ($lbands)" -body {
	pixels aktive op color LCh to Lab \
	    [aktive op pass \
		 [aktive op color Lab to LCh $lab]]
    } -match pixels -result $lbands

    test aktive-op-color-LCh-to-Lab-2.${k}-$cb "aktive op color LCh to Lab, round trip ($cbands)" -body {
	pixels aktive op color Lab to LCh \
	    [aktive op pass \
		 [aktive op color LCh to Lab $lch]]
    } -match pixels -result $cbands
}

unset rgb bands lch cbands cb lab lbands lb k
# # ## ### ##### ######## ############# #####################
cleanupTests
return
