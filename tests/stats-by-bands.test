# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Band statistics (min, max, mean, sum, sum squared, variance, std deviation)

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    max        src
    mean       src
    min        src
    stddev     src
    sum        src
    sumsquared src
    variance   src
} {aktive op band}

# # ## ### ##### ######## ############# #####################
##
## =  1  1.5 =  2  2.5 =  3  3.5
## =  4  4.5 =  5  5.5 =  6  6.5
## =  7  7.5 =  8  8.5 =  9  9.5
## = 10 10.5 = 11 11.5 = 12 12.5

foreach {op expected} {
    max {
	=  1.5  2.5  3.5
	=  4.5  5.5  6.5
	=  7.5  8.5  9.5
	= 10.5 11.5 12.5
    }
    mean {
	=  1.25  2.25  3.25
	=  4.25  5.25  6.25
	=  7.25  8.25  9.25
	= 10.25 11.25 12.25
    }
    min {
	=  1  2  3
	=  4  5  6
	=  7  8  9
	= 10 11 12
    }
    stddev {
	= 0.25 0.25 0.25
	= 0.25 0.25 0.25
	= 0.25 0.25 0.25
	= 0.25 0.25 0.25
    }
    sum {
	=  2.5  4.5  6.5
	=  8.5 10.5 12.5
	= 14.5 16.5 18.5
	= 20.5 22.5 24.5
    }
    sumsquared {
	=   3.25  10.25  21.25
	=  36.25  55.25  78.25
	= 105.25 136.25 171.25
	= 210.25 253.25 300.25
    }
    variance {
	= 0.0625 0.0625 0.0625
	= 0.0625 0.0625 0.0625
	= 0.0625 0.0625 0.0625
	= 0.0625 0.0625 0.0625
    }
} {
    test aktive-op-band-${op}-2.0 "aktive op band $op" -body {
	astcl aktive op band $op [grad]
    } -match image -result [makei op::band::$op 0 0 3 4 1 {} $expected]

    test aktive-op-band-${op}-2.1 "aktive op band $op, dag" -body {
	dag aktive op band $op [grad]
    } -match glob -result "op::band::$op * {} {image::gradient *}"

    if {$op in {
	max min mean sum
    }} {
	# confirm idempotent operations, and simplification

        test aktive-op-band-${op}-2.2 "aktive op band $op, idem potent" -body {
	    astcl aktive op band $op [aktive op pass [aktive op band $op [grad]]]
	} -match image -result [makei op::band::$op 0 0 3 4 1 {} $expected]

        test aktive-op-band-${op}-2.3 "aktive op band $op, reduction, dag" -body {
	    dag aktive op band $op [aktive op band $op [grad]]
	} -match glob -result "op::band::$op * {} {image::gradient *}"
    }

    if {$op in {
	sumsquared
    }} {
	# confirm squarings, and simplifications

        test aktive-op-band-${op}-2.2 "aktive op band $op, chain squared" -body {
	    pixels aktive op band $op \
		[aktive op pass \
		     [aktive op band $op [grad]]]
	} -match pixels -result [pixels aktive op math1 pow [aktive op band $op [grad]] exponent 2]
	# squared (makei op::band::$op 0 0 3 4 1 {} $expected)

        test aktive-op-band-${op}-2.3 "aktive op band $op, chain squared twice" -body {
	    pixels aktive op band $op \
		[aktive op pass \
		     [aktive op band $op  \
			  [aktive op pass \
			       [aktive op band $op [grad]]]]]
	} -match pixels -result [pixels aktive op math1 pow [aktive op band $op [grad]] exponent 4]
	# double squared ( [makei op::band::$op 0 0 3 4 1 {} $expected])

	test aktive-op-band-${op}-2.4 "aktive op band $op, squared, reduced, dag" -body {
	    dag aktive op band $op [aktive op band $op [grad]]
	} -match glob -result "op::math1::pow * {exponent 2.0} {op::band::$op * {} {image::gradient *}}"

        test aktive-op-band-${op}-2.5 "aktive op band $op, squared 2x, reduced, dag" -body {
	    dag aktive op band $op \
		[aktive op band $op  \
		     [aktive op band $op [grad]]]
	} -match glob -result "op::math1::pow * {exponent 4.0} {op::band::$op * {} {image::gradient *}}"
    }

    if {$op in {
	variance stddev
    }} {
	# confirm const results, and simplifications

        test aktive-op-band-${op}-2.2 "aktive op band $op, chain const" -body {
	    pixels aktive op band $op \
		[aktive op pass \
		     [aktive op band $op [grad]]]
	} -match pixels -result [pixels aktive image from value width 3 height 4 depth 1 value 0]

	test aktive-op-band-${op}-2.3 "aktive op band $op, chain const, reduced dag" -body {
	    dag aktive op band $op [aktive op band $op [grad]]
	} -match glob -result "image::from::value * {width 3 height 4 depth 1 value 0.0}"
    }
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
