# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023,2024 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Image montage (adjoin rows, columns, bands)

kt check Tcl     8.6 9
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    x       args
    x-core {src0 src1}
    x-rep  {src args}
    y       args
    y-core {src0 src1}
    y-rep  {src args}
    z       args
    z-core {src0 src1}
    z-rep  {src args}
} {aktive op montage}

# # ## ### ##### ######## ############# #####################

set a [aktive op select x [gradx] from 0 to 4]
set b [aktive op select y [grady] from 0 to 4]

foreach {op w h d expected} {
    x 6 5 1 {
	= 0 1 2 3 4 0 =
	= 0 0 0 0 0 1 =
	= 0 0 0 0 0 2 =
	= 0 0 0 0 0 3 =
	= 0 0 0 0 0 4 =
    }
    y 5 6 1 {
	= 0 1 2 3 4 =
	= 0 0 0 0 0 =
	= 1 0 0 0 0 =
	= 2 0 0 0 0 =
	= 3 0 0 0 0 =
	= 4 0 0 0 0 =
    }
    z 5 5 2 {
	= 0 0 = 1 0 = 2 0 = 3 0 = 4 0 =
	= 0 1 = 0 0 = 0 0 = 0 0 = 0 0 =
	= 0 2 = 0 0 = 0 0 = 0 0 = 0 0 =
	= 0 3 = 0 0 = 0 0 = 0 0 = 0 0 =
	= 0 4 = 0 0 = 0 0 = 0 0 = 0 0 =
    }
} {
    test aktive-op-montage-${op}-2.0 "aktive op montage $op" -body {
	astcl aktive op montage $op $a $b
    } -match image -result [makei op::montage::${op}-core 0 0 $w $h $d {} $expected]

    test aktive-op-montage-${op}-2.1 "aktive op montage $op, dag" -body {
	dag aktive op montage $op $a $b
    } -match glob -result "op::montage::${op}-core * {} {*} {*}"

    test aktive-op-montage-${op}-core-2.0 "aktive op montage ${op}-core" -body {
	astcl aktive op montage ${op}-core $a $b
    } -match image -result [makei op::montage::${op}-core 0 0 $w $h $d {} $expected]

    test aktive-op-montage-${op}-core-2.1 "aktive op montage ${op}-core, dag" -body {
	dag aktive op montage ${op}-core $a $b
    } -match glob -result "op::montage::${op}-core * {} {*} {*}"
}

foreach {op w h d expected} {
    x 10 1 1 {
	= 0 1 2 3 4 = 0 1 2 3 4 =
    }
    y 5 2 1 {
	= 0 1 2 3 4 =
	= 0 1 2 3 4 =
    }
    z 5 1 2 {
	= 0 0 1 1 2 2 3 3 4 4 =
    }
} {
    test aktive-op-montage-${op}-rep-2.0 "aktive op montage ${op}-rep" -body {
	astcl aktive op montage ${op}-rep $a by 2
    } -match image -result [makei op::montage::${op}-core 0 0 $w $h $d {} $expected]
}

# # ## ### ##### ######## ############# #####################

test aktive-op-montage-z-core-3.0 "aktive op montage z-core, translation" -body {
    astcl aktive op montage z \
	[aktive image from value width 2 height 1 depth 1 value 11] \
	[aktive op location move to \
	     [aktive image from value width 4 height 1 depth 1 value 22] x -1 y -1]
} -match image -result [makei op::montage::z-core 0 0 4 1 2 {} {
    = 11 22 = 11 22 = 0 22 = 0 22
}]

# # ## ### ##### ######## ############# #####################
unset a b op w h d expected
cleanupTests
return
