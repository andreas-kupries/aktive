# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Swapping two axes (xy, xz, yz)

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    xy src
    xz src
    yz src
} {aktive op swap}

# # ## ### ##### ######## ############# #####################
##
# input/gradient 3x4x2
##
##       0 ....   1......   2...... - 3 columns
##     --------  --------  --------
#	 1  1.5    2  2.5    3  3.5  0
#	 4  4.5    5  5.5    6  6.5  1
#	 7  7.5    8  8.5    9  9.5  2
#	10 10.5   11 11.5   12 12.5  3
##     --- ----  --- ----  --- ----  \ 4 rows
##      0. 1...   0. 1...   0. 1... - 2 bands
#
# Swapping columns, rows, or bands

foreach {k op w h d expected} {
    0 xy 4 3 2 {
	= 1 1.5 = 4 4.5 = 7 7.5 = 10 10.5 =
	= 2 2.5 = 5 5.5 = 8 8.5 = 11 11.5 =
	= 3 3.5 = 6 6.5 = 9 9.5 = 12 12.5 =
    }
    1 xz 2 4 3 {
	=  1  2  3 =  1.5  2.5  3.5 =
	=  4  5  6 =  4.5  5.5  6.5 =
	=  7  8  9 =  7.5  8.5  9.5 =
	= 10 11 12 = 10.5 11.5 12.5 =
    }
    2 yz 3 2 4 {
	= 1   4   7   10   = 2   5   8   11   = 3   6   9   12   =
	= 1.5 4.5 7.5 10.5 = 2.5 5.5 8.5 11.5 = 3.5 6.5 9.5 12.5 =
    }
} {
    unset -nocomplain map
    lappend map /fun    $op
    lappend map /pixels $expected
    lappend map /w      $w
    lappend map /h      $h
    lappend map /d      $d

    test aktive-op-swap-${op}-2.0 "aktive op swap $op" -body {
	astcl aktive op swap $op [grad]
    } -match image -result [string map $map {
	type   op::swap::/fun
	domain {x 0 y 0 width /w height /h depth /d}
	pixels {/pixels}
    }]

    test aktive-op-swap-${op}-2.1 "aktive op swap $op, dag" -body {
	dag aktive op swap $op [grad]
    } -match glob -result [string map $map {op::swap::/fun * {} {image::gradient *}}]

    set in [grad]
    set id [aktive query id $in]

    test aktive-op-swap-${op}-2.2 "aktive op swap $op, self-complementary, chain reduction" -body {
	dag aktive op swap $op [aktive op swap $op $in]
    } -match glob -result "image::gradient $id *"

    unset map in id
}

# TODO :: check with sources translated off the main diagonal

# # ## ### ##### ######## ############# #####################
cleanupTests
return
