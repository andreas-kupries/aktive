# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Swapping two axes (xy, xz, yz)

kt check Tcl     8.6
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    xy src
    xz src
    yz src
} {aktive op swap}

# # ## ### ##### ######## ############# #####################
##
# input/gradient 3x4x2
##
##       0 ....   1......   2...... - 3 columns
##     --------  --------  --------
#	 1  1.5    2  2.5    3  3.5  0
#	 4  4.5    5  5.5    6  6.5  1
#	 7  7.5    8  8.5    9  9.5  2
#	10 10.5   11 11.5   12 12.5  3
##     --- ----  --- ----  --- ----  \ 4 rows
##      0. 1...   0. 1...   0. 1... - 2 bands
#
# Swapping columns, rows, or bands

foreach {op domain expected} {
    xy {4 3 2} {
	= 1 1.5 = 4 4.5 = 7 7.5 = 10 10.5 =
	= 2 2.5 = 5 5.5 = 8 8.5 = 11 11.5 =
	= 3 3.5 = 6 6.5 = 9 9.5 = 12 12.5 =
    }
    xz {2 4 3} {
	=  1  2  3 =  1.5  2.5  3.5 =
	=  4  5  6 =  4.5  5.5  6.5 =
	=  7  8  9 =  7.5  8.5  9.5 =
	= 10 11 12 = 10.5 11.5 12.5 =
    }
    yz {3 2 4} {
	= 1   4   7   10   = 2   5   8   11   = 3   6   9   12   =
	= 1.5 4.5 7.5 10.5 = 2.5 5.5 8.5 11.5 = 3.5 6.5 9.5 12.5 =
    }
} {
    test aktive-op-swap-${op}-2.0 "aktive op swap $op" -body {
	astcl aktive op swap $op [grad]
    } -match image -result [makei op::swap::$op 0 0 {*}$domain {} $expected]

    test aktive-op-swap-${op}-2.1 "aktive op swap $op, dag" -body {
	dag aktive op swap $op [grad]
    } -match glob -result "op::swap::$op * {} {image::gradient *}"

    set in [grad]
    set id [aktive query id $in]

    test aktive-op-swap-${op}-2.2 "aktive op swap $op, self-complementary, chain reduction" -body {
	dag aktive op swap $op [aktive op swap $op $in]
    } -match glob -result "image::gradient $id *"

    # note: 2.3 below
    test aktive-op-swap-${op}-2.4 "aktive op swap $op, translated" -body {
	astcl aktive op swap $op [aktive op location move to [grad] x 10 y 3]
    } -match image -result [makei op::swap::$op 10 3 {*}$domain {} $expected]

    ##
    # - -- --- ----- -------- ------------- ---------------------
    ## Slice and check in all directions

    lassign $domain w h d

    for {set row 0} {$row < $h} {incr row} {
	test aktive-op-swap-${op}-2.3.r$row "aktive op swap $op, row slice" -body {
	    pixels aktive op select y [aktive op swap $op [grad]] from $row
	} -match pixels -result [slice-row $row $w $h $d $expected]
    }

    for {set col 0} {$col < $w} {incr col} {
	test aktive-op-swap-${op}-2.3.c$col "aktive op swap $op, column slice" -body {
	    pixels aktive op select x [aktive op swap $op [grad]] from $col
	} -match pixels -result [slice-col $col $w $h $d $expected]
    }

    for {set band 0} {$band < $d} {incr band} {
	test aktive-op-swap-${op}-2.3.b$band "aktive op swap $op, band slice" -body {
	    pixels aktive op select z [aktive op swap $op [grad]] from $band
	} -match pixels -result [slice-band $band $w $h $d $expected]
    }

    # - -- --- ----- -------- ------------- ---------------------
    unset in id w h d row col band
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
