# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023,2024 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Flipping (reversing) of rows, columns, bands

kt check Tcl     8.6 9
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    x src
    y src
    z src
} {aktive op flip}

# # ## ### ##### ######## ############# #####################
##
# input/gradient 3x4x2
##
##       0 ....   1......   2...... - 3 columns
##     --------  --------  --------
#	 1  1.5    2  2.5    3  3.5  0
#	 4  4.5    5  5.5    6  6.5  1
#	 7  7.5    8  8.5    9  9.5  2
#	10 10.5   11 11.5   12 12.5  3
##     --- ----  --- ----  --- ----  \ 4 rows
##      0. 1...   0. 1...   0. 1... - 2 bands
#
# Fliping columns, rows, or bands

foreach {op expected} {
    x {
	=  3.00  3.50 =  2.00  2.50 =  1.00  1.50
	=  6.00  6.50 =  5.00  5.50 =  4.00  4.50
	=  9.00  9.50 =  8.00  8.50 =  7.00  7.50
	= 12.00 12.50 = 11.00 11.50 = 10.00 10.50
    }
    y {
	= 10.00 10.50 = 11.00 11.50 = 12.00 12.50
	=  7.00  7.50 =  8.00  8.50 =  9.00  9.50
	=  4.00  4.50 =  5.00  5.50 =  6.00  6.50
	=  1.00  1.50 =  2.00  2.50 =  3.00  3.50
    }
    z {
	=  1.50  1.00 =  2.50  2.00 =  3.50  3.00
	=  4.50  4.00 =  5.50  5.00 =  6.50  6.00
	=  7.50  7.00 =  8.50  8.00 =  9.50  9.00
	= 10.50 10.00 = 11.50 11.00 = 12.50 12.00
    }
} {
    test aktive-op-flip-${op}-2.0 "aktive op flip $op" -body {
	astcl aktive op flip $op [grad]
    } -match image -result [makei op::flip::$op 0 0 3 4 2 {} $expected]

    test aktive-op-flip-${op}-2.1 "aktive op flip $op, dag" -body {
	dag aktive op flip $op [grad]
    } -match glob -result "op::flip::$op * {} {image::gradient *}"

    set in [grad]
    set id [aktive query id $in]

    test aktive-op-flip-${op}-2.2 "aktive op flip $op, self-complementary, chain reduction" -body {
	dag aktive op flip $op [aktive op flip $op $in]
    } -match glob -result "image::gradient $id *"

    unset in id

    test aktive-op-flip-${op}-2.3 "aktive op flip $op, partial view" -body {
	pixels aktive op select $op [aktive op flip $op [grad$op]] from 5 to 10
    } -match pixels -result {14 13 12 11 10 9}

    test aktive-op-flip-${op}-2.4 "aktive op flip $op, translated" -body {
	astcl aktive op flip $op [aktive op location move to [grad] x 5 y 5]
    } -match image -result [makei op::flip::$op 5 5 3 4 2 {} $expected]
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
