# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2023,2024 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## AKTIVE. Grey-level morphology operations.

kt check Tcl     8.6 9
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt local testing aktive

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
## Syntax

syntax {
    close  {src args}
    dilate {src args}
    erode  {src args}
    open   {src args}
    toggle {src args}
} {aktive op morph}

syntax {
    all      {src args}
    external {src args}
    internal {src args}
} {aktive op morph gradient}

syntax {
    black    {src args}
    white    {src args}
} {aktive op morph tophat}

# # ## ### ##### ######## ############# #####################
##

set in [aktive image cross radius 3 width 1]

# radius 3 = 7x7 + embed = 9x9, cross - result is 7x7
#
# _ _ * * * _ _
# _ _ * * * _ _
# * * * * * * *
# * * * * * * *
# * * * * * * *
# _ _ * * * _ _
# _ _ * * * _ _

foreach {op expected} {
    close {
	_ _ _ _ _ _ _
	_ _ * * * _ _
	_ * * * * * _
	_ * * * * * _
	_ * * * * * _
	_ _ * * * _ _
	_ _ _ _ _ _ _
    }
    dilate {
	_ * * * * * _
	* * * * * * *
	* * * * * * *
	* * * * * * *
	* * * * * * *
	* * * * * * *
	_ * * * * * _
    }
    erode {
	 _ _ _ _ _ _ _
	 _ _ _ * _ _ _
	 _ _ _ * _ _ _
	 _ * * * * * _
	 _ _ _ * _ _ _
	 _ _ _ * _ _ _
	 _ _ _ _ _ _ _
    }
    open {
	_ _ * * * _ _
	_ _ * * * _ _
	* * * * * * *
	* * * * * * *
	* * * * * * *
	_ _ * * * _ _
	_ _ * * * _ _
    }
    toggle {
	_ / / / / / _
	/ / / * / / /
	/ / / * / / /
	/ * * * * * /
	/ / / * / / /
	/ / / * / / /
	_ / / / / / _
    }
    {gradient all} {
	_ * * * * * _
	* * * _ * * *
	* * * _ * * *
	* _ _ _ _ _ *
	* * * _ * * *
	* * * _ * * *
	_ * * * * * _
    }
    {gradient external} {
	_ * _ _ _ * _
	* * _ _ _ * *
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	* * _ _ _ * *
	_ * _ _ _ * _
    }
    {gradient internal} {
	_ _ * * * _ _
	_ _ * _ * _ _
	* * * _ * * *
	* _ _ _ _ _ *
	* * * _ * * *
	_ _ * _ * _ _
	_ _ * * * _ _
    }
    {tophat white} {
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
	_ _ _ _ _ _ _
    }
    {tophat black} {
	_ _ % % % _ _
	_ _ _ _ _ _ _
	% _ _ _ _ _ %
	% _ _ _ _ _ %
	% _ _ _ _ _ %
	_ _ _ _ _ _ _
	_ _ % % % _ _
    }
} {
    test aktive-image-${op}-2.0 "aktive op morph $op, default radius" -body {
	pixels aktive op morph {*}$op $in
    } -match pixels -result [string map {_ 0 * 1 / 0.5 % -1} $expected]
}

unset in

# # ## ### ##### ######## ############# #####################
cleanupTests
return
